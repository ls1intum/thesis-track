
plugins {
	id "java"
	id "idea"
	id "com.gorylenko.gradle-git-properties" version "2.4.2"
	id "com.github.andygoossens.modernizer" version "1.10.0"
	id "org.springframework.boot" version "3.3.5"
	id "io.spring.dependency-management" version "1.1.6"
	id "com.github.ben-manes.versions" version "0.51.0"
}

group = "thesis-track.ls1"
version = "3.0.0"
description = "Thesis Track"

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-mail"
	implementation "org.springframework.boot:spring-boot-starter-webflux"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"

	implementation "org.liquibase:liquibase-core:4.30.0"
	implementation "org.postgresql:postgresql"

	implementation "commons-io:commons-io:2.17.0"
	implementation "com.github.vladimir-bukhtoyarov:bucket4j-core:8.0.1"
	implementation "org.mnode.ical4j:ical4j:4.0.5"
	implementation "com.itextpdf:itext-core:8.0.5"
	implementation "com.itextpdf:html2pdf:5.0.5"
	implementation "com.auth0:java-jwt:4.4.0"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
	testImplementation 'org.mockito:mockito-core:5.14.2'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.3'
	testImplementation 'org.testcontainers:postgresql:1.20.3'

	// TODO: for some reason an update of org.junit.jupiter:junit-jupiter-engine to 5.11.x breaks the tests
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.5'
	testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.11.3"
	compileOnly "org.projectlombok:lombok:1.18.36"
	annotationProcessor "org.projectlombok:lombok:1.18.36"
}

test {
	useJUnitPlatform()

	maxParallelForks = 1
	forkEvery = 0

	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
	}
	testLogging.showStandardStreams = true
	systemProperty 'spring.profiles.active', 'test'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:deprecation"
}

// Available commands:
//
// Find dependency updates:    ./gradlew dependencyUpdates -Drevision=release
// Test:                       ./gradlew test
// Build:                      ./gradlew build -x test
// Run:						   ./gradlew bootRun
